;***********************************************************************************************
; 功  能:	在DP_51PRO实验仪上利用定时器测量电机的转速，并将测量值显示到图形液晶显示器(12864)上。
; 注  意:	1.必须将LCMDRV_12864.LIB加载到工程中。
;		2.在使用12864前，必须先调用_LCM_Init()完成对LCM12864的初始化。R7:0 不显示光标，1 不显示光标
;		3.使用_LCM_DispUIntXY()在指定位置显示数值。R7:行(0-7)；R5:列(0-20)；R2R3:无符号16位二进制数(高位在R2，低位在R3)。
;***********************************************************************************************
;_利用定时 计数器测量电动机的转速
$INCLUDE(LCMDRV_12864.INC)			;包含库程序定义
;资源定义 **************************************
;位变量 ****************************************
REFRESHDISP	EQU	08H			;LCMDRV_12864刷新显示标志 不为0刷新

;字节变量 **************************************
SPEED_CNTH	EQU	30H			;速度脉冲计数高8位
SPEED_CNTL	EQU	31H			;速度脉冲计数低8位
SECOND_CNT	EQU	32H			;秒计数(计20次50ms)

VAR_END		EQU	32H			;最后一个字节变量的位置
;堆栈定义 **************************************
STACK_INI	EQU	VAR_END+1		;须保证足够的空间

;程序 ******************************************
		ORG	0000H			;复位入口
		LJMP	RESET		
		ORG	0003H			;外部中断0 服务程序入口
		RETI
		ORG	000BH			;定时器/计数器0 溢出中断服务程序入口 计数器
		RETI
		ORG	0013H			;外部中断1 服务程序入口
		RETI
		ORG	001BH			;定时器/计数器1 溢出中断服务程序入口 定时器50ms
		LJMP	INT_T1	
		ORG	0023H			;串行口中断服务程序入口
		RETI
		ORG	0040H			;复位处理程序
RESET:		MOV	SP,#STACK_INI		;设置栈底指针
;初始化外部设备 ********************************
		MOV	R7,#00H			;初始化显示器(不显示光标)
		LCALL	_LCM_Init
;中断设置 **************************************
		SETB	ET1			;中断允许
		SETB	PT1			;中断优先级
;定时/计数设置 *********************************
		MOV	TMOD,#00010101B
		MOV	TH0,#00H		;计数器0计数初值0000H
		MOV	TL0,#00H
		MOV	DPTR,#65536-50000	;50ms = (2^16-计数初值)*定时周期
		MOV	TH1,DPH
		MOV	TL1,DPL
;初始化变量 ************************************
		CLR	REFRESHDISP		;清刷新显示标志
		MOV	SECOND_CNT,#20		;置秒计数初值
;启动内部定时 **********************************
		SETB	TR0			;启动计数器0
		SETB	TR1			;启动定时器1
;开中断 ****************************************
		SETB	EA
;主程序 ****************************************
MAIN:		JNB	REFRESHDISP,$		;判断是否需要刷新显示 为0死循环，等待中断
		CLR	TR1			;停止定时器1
		CLR	REFRESHDISP		;清刷新显示标志
		MOV	R7,#01			;从第2行第8列开始显示速度值(5位数值)
		MOV	R5,#07
		MOV	R2,SPEED_CNTH
		MOV	R3,SPEED_CNTL
		CALL	_LCM_DispUIntXY
		SETB	TR1			;启动定时器1
		SJMP	MAIN
;内部定时中断服务程序 **************************
INT_T1:
		CLR	TF1			;清定时器中断标志位
		CLR	TR1			;停止定时器1
		;重置定时 计数初值
		MOV	DPTR,#65536-50000
		MOV	TH1,DPH
		MOV	TL1,DPL
		;此处添加保护现场内容的指令(如果有的话)
		
		;此处添加判断一秒钟时间到的指令, 如果没到时间则跳转恢复现场,
		DJNZ	SECOND_CNT,Exit
		;到1s,执行下面的指令。
		CLR	TR0			;停止计数器0
		MOV	SECOND_CNT,#20		;置秒计数初值
		
		MOV	SPEED_CNTL,TL0		;记录速度检测计数(供主程序显示用)
		MOV	SPEED_CNTH,TH0
		MOV	TL0, #00		;速度检测计数归零
		MOV	TH0, #00
		SETB	REFRESHDISP		;设置刷新显示标志 (控制主程序刷新显示)
		
		SETB	TR0			;启动计数器0
Exit:
		SETB	TR1			;启动定时器1
		RETI
;程序结束 **************************************
		END
